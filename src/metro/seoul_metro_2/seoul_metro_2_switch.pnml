/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/

// Cargo subtype
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_cargo_subtype_text, cargo_subtype) {
    0: return string(STR_REFIT_METRO_2K_VVVF_4TH_NAME);
    1: return string(STR_REFIT_METRO_2K_VVVF_3RD_NAME);
    2: return string(STR_REFIT_METRO_2K_VVVF_2ND_NAME);
    3: return string(STR_REFIT_METRO_2K_VVVF_1ST_NAME);
    4: return string(STR_REFIT_METRO_2K_WIDE_NAME);
    5: return string(STR_REFIT_METRO_2K_RECTANGLE_NAME);
    6: return string(STR_REFIT_METRO_2K_RECTANGLE_MODIFIED_NAME);
    return string(STR_REFIT_METRO_2K_VVVF_4TH_NAME);
}

// Engine
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_engineH, cargo_subtype) {
    0: set_2K_VVVF_4TH_engineH;
    1: set_2K_VVVF_3RD_engineH;
    2: set_2K_VVVF_2ND_engineH;
    3: set_2K_VVVF_1ST_engineH;
    4: set_2K_WIDE_engineH;
    5: set_2K_RECTANGLE_engineH;
    6: set_2K_RECTANGLE_MODIFIED_engineH;
    set_2K_VVVF_4TH_engineH;
}
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_engineT, cargo_subtype) {
    0: set_2K_VVVF_4TH_engineT;
    1: set_2K_VVVF_3RD_engineT;
    2: set_2K_VVVF_2ND_engineT;
    3: set_2K_VVVF_1ST_engineT;
    4: set_2K_WIDE_engineT;
    5: set_2K_RECTANGLE_engineT;
    6: set_2K_RECTANGLE_MODIFIED_engineT;
    set_2K_VVVF_4TH_engineT;
}

// Wagon
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_wagon_panto_cargo_subtype, cargo_subtype) {
    0: set_2K_VVVF_4TH_wagon_panto;
    1: set_2K_VVVF_3RD_wagon_panto;
    2: set_2K_VVVF_2ND_wagon_panto;
    3: set_2K_VVVF_1ST_wagon_panto;
    4: set_2K_WIDE_wagon_panto;
    5: set_2K_RECTANGLE_wagon_panto;
    6: set_2K_RECTANGLE_MODIFIED_wagon_panto;
    set_2K_VVVF_4TH_wagon_panto;
}
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_wagon_cargo_subtype, cargo_subtype) {
    0: set_2K_VVVF_4TH_wagon;
    1: set_2K_VVVF_3RD_wagon;
    2: set_2K_VVVF_2ND_wagon;
    3: set_2K_VVVF_1ST_wagon;
    4: set_2K_WIDE_wagon;
    5: set_2K_RECTANGLE_wagon;
    6: set_2K_RECTANGLE_MODIFIED_wagon;
    set_2K_VVVF_4TH_wagon;
}

// Engine
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_engine, position_in_consist_from_end == 0 && num_vehs_in_consist > 1) {
    1: sw_SEOUL_METRO_2_engineT;
    sw_SEOUL_METRO_2_engineH;
}

// Pantograh - default
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_panto_wagon_default, (position_in_consist + 1) % 3) {
    0: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    sw_SEOUL_METRO_2_wagon_cargo_subtype;
}

// Pantograph - 10 cars (TC - M - M' - T - M' - T - T - M - M' - TC)
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_panto_wagon_for_10cars, (position_in_consist + 1) % 10) {
    3: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    5: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    9: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    sw_SEOUL_METRO_2_wagon_cargo_subtype;
}
// Pantograph - 10 cars (MC - M' - T - T - M' - M - T - T - M' - MC)
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_panto_wagon_for_10cars_2, (position_in_consist + 1) % 10) {
    2: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    5: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    9: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    sw_SEOUL_METRO_2_wagon_cargo_subtype;
}
// Pantograph - 4 cars (MC - M' - M' - MC)
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_panto_wagon_for_4cars, (position_in_consist + 1) % 4) {
    2: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    3: sw_SEOUL_METRO_2_wagon_panto_cargo_subtype;
    sw_SEOUL_METRO_2_wagon_cargo_subtype;
}

// Pantograph for Rheostatic cars
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_panto_wagon_RHEO_by_length, num_vehs_in_consist) {
    4:  sw_SEOUL_METRO_2_panto_wagon_for_4cars;
    6:  sw_SEOUL_METRO_2_panto_wagon_for_10cars_2;
    10: sw_SEOUL_METRO_2_panto_wagon_for_10cars_2;
    sw_SEOUL_METRO_2_panto_wagon_default;
}

// Pantograph for Seoul Metro 2k CHOPPER cars
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_panto_wagon_CHOPPER_by_length, num_vehs_in_consist) {
    6:  sw_SEOUL_METRO_2_panto_wagon_for_10cars;
    10: sw_SEOUL_METRO_2_panto_wagon_for_10cars;
    sw_SEOUL_METRO_2_panto_wagon_default;
}

// Pantograph for VVVF cars
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_panto_wagon_VVVF_by_length, num_vehs_in_consist) {
    4:  sw_SEOUL_METRO_2_panto_wagon_for_4cars;
    6:  sw_SEOUL_METRO_2_panto_wagon_for_10cars;
    10: sw_SEOUL_METRO_2_panto_wagon_for_10cars;
    sw_SEOUL_METRO_2_panto_wagon_default;
}

// Pantograph
switch(FEAT_TRAINS, SELF, sw_SEOUL_METRO_2_wagon, cargo_subtype) {
    0..3: sw_SEOUL_METRO_2_panto_wagon_VVVF_by_length;
    5..6: sw_SEOUL_METRO_2_panto_wagon_RHEO_by_length;
    4:    sw_SEOUL_METRO_2_panto_wagon_CHOPPER_by_length;
    sw_SEOUL_METRO_2_panto_wagon_VVVF_by_length;
}
